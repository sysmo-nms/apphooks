apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'jdepend'
apply plugin: 'application'

sourceCompatibility = 1.7
group = "io.sysmo"
archivesBaseName = "apphooks"
mainClassName = 'io.sysmo.apphooks.impl.DevelopmentImpl'
/*
 * "gradle uploadArchives" will upload:
 * version = "X.X.X"          -> a new release
 * version = "X.X.X-SNAPSHOT" -> a new snapshot
 */
version = "0.1.0-SNAPSHOT"

repositories {
    mavenCentral()
}


jdepend  { ignoreFailures = true }
pmd      { ignoreFailures = true }

findbugs { ignoreFailures = true }
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

javadoc {
    options.overview = "overview.html"
    options.docTitle = "AppHooks"
    options.windowTitle = "AppHooks"
}

dependencies {
    testCompile 'junit:junit:4.2'
    compile group: 'org.quartz-scheduler', name: 'quartz', version: '2.2.3'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
    compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.12'
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'AppHooks glue utils'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'AppHooks glue utils'
                url 'http://www.sysmo.io/apphooks'

                scm {
                    connection 'scm:git:git@github.com:://github.com:sysom-nms/apphooks.git'
                    developerConnection 'scm:git:git@github.com:://github.com:sysmo-nms/apphooks.git'
                    url 'git@github.com:sysmo-nms/apphooks.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'ssbx'
                        name 'Sebastien Serre'
                        email 'ssbx@sysmo.io'
                    }
                }
            }
        }
    }
}
